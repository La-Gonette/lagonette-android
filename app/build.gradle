apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("app/keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId "org.lagonette.app"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 3
        versionName "1.0.0-alpha3"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/build/schemas".toString()]
            }
        }

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"

            buildConfigField "boolean", "LOG_SQL", "true"
            buildConfigField "boolean", "FILL_DB_WITH_LOCAL_VALUE", "true"
            buildConfigField "boolean", "FILL_DB_WITH_SERVER_VALUE", "true"

//             buildConfigField "String", "HOST", '"http://82.225.211.150:18001/dolibarr/htdocs/"'
            buildConfigField "String", "HOST", '"https://dolibarr.lagonette.org/"'
        }
        prod.initWith(buildTypes.release)
        prod {
            applicationIdSuffix ".prod"
            versionNameSuffix "-prod"

            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.release //TODO really usefull ?

            buildConfigField "boolean", "LOG_SQL", "true"
            buildConfigField "boolean", "FILL_DB_WITH_LOCAL_VALUE", "true"
            buildConfigField "boolean", "FILL_DB_WITH_SERVER_VALUE", "true"

            buildConfigField "String", "HOST", '"https://dolibarr.lagonette.org/"'
        }
        release {
            minifyEnabled true
            shrinkResources false //TODO
            signingConfig signingConfigs.release

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField "boolean", "LOG_SQL", "false"
            buildConfigField "boolean", "FILL_DB_WITH_LOCAL_VALUE", "true"
            buildConfigField "boolean", "FILL_DB_WITH_SERVER_VALUE", "true"

            buildConfigField "String", "HOST", '"https://dolibarr.lagonette.org/"'
        }
    }
}

repositories {
    maven {
        url 'https://maven.google.com'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'

    implementation 'com.google.android.gms:play-services-maps:15.0.1'
    implementation 'com.google.android.gms:play-services-location:15.0.1'
    implementation 'com.google.maps.android:android-maps-utils:0.5'

    implementation 'com.googlecode.libphonenumber:libphonenumber:8.4.2'

    // For Crashlytics
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.2'

    // For Lifecycles, LiveData, and ViewModel
    implementation 'android.arch.lifecycle:runtime:1.1.1'
    implementation 'android.arch.lifecycle:extensions:1.1.1'
    annotationProcessor "android.arch.lifecycle:compiler:1.1.1"

    // For Room
    implementation 'android.arch.persistence.room:runtime:1.0.0'
    annotationProcessor "android.arch.persistence.room:compiler:1.0.0"

    // For Paging library
    implementation "android.arch.paging:runtime:1.0.0-rc1"

    // For Moshi
    implementation 'com.squareup.moshi:moshi:1.5.0'

    // For Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.3.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.8.0'

    // For Glide
    implementation 'com.github.bumptech.glide:glide:3.8.0'

    //For Chain Adapter
    implementation project(":chainadapter")

    //For Identifier
    implementation project(":identifier")

    //For Functions
    implementation project(":functions")

}

apply plugin: 'com.google.gms.google-services'
